plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id "androidx.navigation.safeargs.kotlin"
    id "com.google.android.gms.oss-licenses-plugin"
}

apply from: "../ktlint.gradle"
apply from: '../android_commons.gradle'

android {
    defaultConfig {
        archivesBaseName = 'WeatherApp'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // API Base URL
        buildConfigField "String", "API_BASE_URL", "\"http://api.openweathermap.org/data/2.5/\""

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            applicationId "com.github.dhaval2404.weatherapp.dev"
            versionCode 2
            versionName "1.2"
        }

        staging {
            applicationId "com.github.dhaval2404.weatherapp.staging"
            versionCode 1
            versionName "1.0"
        }

        production {
            applicationId "com.github.dhaval2404.weatherapp"
            versionCode 1
            versionName "1.0"
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

dependencies {
    implementation project(":data:room")
    implementation project(":data:remote")
    implementation project(":data:repository")

    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Lottie Animation
    implementation "com.airbnb.android:lottie:3.7.0"

    // Koin ViewModel
    implementation "org.koin:koin-androidx-viewmodel:2.2.2"

    // Image loading library
    implementation "io.coil-kt:coil:1.1.1"

    // Navigation Components
    def nav_version = "2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Google Map & Place Picker
    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    implementation 'com.google.android.libraries.places:places:2.4.0'

    // Include open source notices
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
}